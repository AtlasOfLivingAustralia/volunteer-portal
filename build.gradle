import org.apache.tools.ant.filters.ReplaceTokens

import static org.springframework.boot.gradle.SpringBootPluginExtension.LayoutType.*

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath "org.postgresql:postgresql:$postgresVersion" // for flyway
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
    id 'nu.studer.jooq' version '3.0.3'
    id 'pl.codelabs.flywayJooq' version '1.3.1'
    //    id "com.moowork.node" version "1.1.1"
    // Gradle Fix for Windows long paths (i.e. Java installed in Program Files/Java/...etc
    id "com.virgo47.ClasspathJar" version "1.0.0"
}

version "6.1.4-SNAPSHOT"
group "au.org.ala"
description "Digivol application"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"
apply from: 'https://raw.githubusercontent.com/AtlasOfLivingAustralia/travis-build-configuration/master/travis_grails_publish.gradle'

def ci = System.env.CI == 'true'
def localProps = file("$project.rootDir/local.properties")
if (localProps.exists()) {
    localProps.withReader {
        def props = new Properties()
        props.load(it)
        project.ext.local = props
    }
} else {
    logger.error("No local.properties file found for flyway/jooq")
    project.ext.local = new Properties()
}
flyway {
    url = project.ext.local.flywayUrl
    user = project.ext.local.flywayUsername
    password = project.ext.local.flywayPassword
    baselineOnMigrate = project.ext.local.flywayBaselineOnMigrate?.toBoolean() ?: false
    outOfOrder = project.ext.local.flywayOutOfOrder?.toBoolean() ?: false
}
jooq {
//        version = "${ext['jooq.version']}"
//        edition = 'OSS'
    volunteer(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = project.ext.local.flywayUrl
            user = project.ext.local.flywayUsername
            password = project.ext.local.flywayPassword
        }
        generator {
            name = 'org.jooq.codegen.JavaGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                forcedTypes {
                    forcedType {
                        userType = 'java.lang.Object'
                        binding = 'au.org.ala.volunteer.jooq.PostgresJSONMoshiBinding'
                        expression = '.*' // jooq 3.12 -> includeExpression
                        types = 'JSONB?' // jooq 3.12 -> includeTypes
                    }
                }
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
                // ...
            }
            target {
                packageName = 'au.org.ala.volunteer.jooq'
                // directory = ...
            }
        }
    }
}
generateVolunteerJooqSchemaSource.dependsOn flywayMigrate

// override spring boot dependency management versions here
ext {
//    set "tomcat.version", "8.5.20" // use this to force a tomcat version
    set "jooq.version", '3.11.9'
}

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
    jcenter()
}

// not in 3.2.7 by default?
//dependencyManagement {
//    imports {
//        mavenBom "org.grails:grails-bom:$grailsVersion"
//    }
//    applyMavenExclusions false
//}

dependencies {
    // Grails 3.3.15
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache:4.0.3"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
    compile "org.hibernate:hibernate-core:$hibernateVersion"
    compile "org.grails.plugins:gsp"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "org.glassfish.web:el-impl:2.1.2-b03"

    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails.plugins:geb:1.1.2"
    testCompile "org.grails:grails-web-testing-support"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
    testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:2.47.1"

    // Extra official Grails plugins
    compile "org.grails.plugins:converters"
    compile "com.bertramlabs.plugins:asset-pipeline-grails:$assetPipelineVersion"
    assets "com.bertramlabs.plugins:sass-asset-pipeline:$assetPipelineVersion"
    //testCompile "org.grails:grails-plugin-testing" // Moved in upgrade to Grails 3.3.11
    testCompile "org.grails:grails-test-mixins:3.3.0" // For legacy @TestMixin and @TestFor

    compile "org.grails:grails-async-gpars"
    compile "org.grails:grails-events-rxjava2"
    compile "org.grails.plugins:grails-java8"
    compile "org.grails.plugins:rxjava2:2.0.0"
    compile "org.codehaus.gpars:gpars:1.2.1" // Added for the GPars dependencies.
    compile "io.reactivex.rxjava2:rxjava:2.1.3"
    compile "org.grails:grails-datastore-gorm-async:${gormVersion}"

    // Extra Hibernate
    compile "org.hibernate:hibernate-ehcache:$hibernateVersion"
    compile "org.hibernate:hibernate-java8:$hibernateVersion"

    // Additional Plugins
    compile 'org.grails.plugins:cache-headers:2.0.2'
    compile 'org.grails.plugins:csv:1.0.ALA.3'
    compile 'org.grails.plugins:postgresql-extensions:6.1.0'
    compile 'org.grails.plugins:mail:2.0.1'
    compile 'org.grails.plugins:grails-executor:0.4' // used in TaskLoadService.backgroundProcessQueue
    compile 'org.grails.plugins:grails-markdown:3.0.0' // may need modifications?
    compile 'org.grails.plugins:grails-pretty-time:4.0.0' // may need modifications?
    compile 'org.grails.plugins:quartz:2.0.12'
    compile 'org.grails.plugins:grails-google-visualization:2.2'
    compile 'org.grails.plugins:external-config:1.1.2'
    // TODO ALA Plugins: image service (is it used anywhere?)
    compile 'org.grails.plugins:ala-auth:3.2.3'
    compile 'au.org.ala:userdetails-service-client:1.5.0' // override this to for external properties changes
    compile 'org.grails.plugins:cache-ehcache:3.0.0'
    compile 'org.ehcache:ehcache:3.4.0' // Prevent cache-ehcache dependent version being overridden
    //compile 'org.grails.plugins:cache-ehcache:2.0.0.ALA'
    // TODO Collection Event Controller?

    // Dependencies
    runtime "org.postgresql:postgresql:$postgresVersion"
    runtime "com.zaxxer:HikariCP:4.0.3"
    compile "com.drewnoakes:metadata-extractor:2.11.0"
    compile 'org.imgscalr:imgscalr-lib:4.2'
    compile 'com.squareup.retrofit2:retrofit:2.4.0'
    compile 'com.squareup.retrofit2:converter-gson:2.4.0'
    compile 'com.google.guava:guava:19.0'
    compile 'com.github.ben-manes.caffeine:caffeine:2.8.0'
    compile 'org.apache.commons:commons-compress:1.11'
    compile 'org.apache.commons:commons-pool2:2.4.2'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile 'org.elasticsearch:elasticsearch:1.3.5'
    // optional for elastic search, version should match elastic search optional dep
    compile group: 'org.apache.lucene', name: 'lucene-expressions', version: '4.9.1'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'org.freemarker:freemarker:2.3.23'
    compile 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20160526.1-ALA'
    compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'

    // DB migrations, etc
    compile 'org.jooq:jooq'
    jooqRuntime "org.postgresql:postgresql:$postgresVersion"
    compile "org.flywaydb:flyway-core:4.2.0"

    // For logback filter
    compile 'org.codehaus.janino:janino:3.1.7'
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always', '-Xmx6000m')
//    addResources = true // enable for reloading src/main/resources when using gradle bootRun, disables processResources
}

springBoot {
    layout = WAR
}

bootRepackage {
    enabled = true
    executable = true
    classifier = 'exec'

    embeddedLaunchScriptProperties = [
                    initInfoDescription: project.description,
                    initInfoShortDescription: project.name,
                    initInfoProvides: jar.baseName,
//                    mode: 'service'
            ]
}

assets {
    minifyJs = true
    minifyCss = true
    enableSourceMaps = true
    includes = ['tinymce/**/*', 'compile/**/*.ttf', 'compile/**/*.otf', 'compile/**/*.eot', 'compile/**/*.svg', 'compile/**/*.woff', 'compile/**/*.woff2', 'compile/**/*.png', 'compile/**/*.jpg', 'compile/**/*.gif']
    excludes = ['compile/*', 'compile/**/*']
//    excludes = ['codemirror/**/*.html', "**/*.min.js", "**/*.min.css", "angular/*/i18n/**", "**/*.map"]
//    includes = ["fonts/*", "node_modules/*", "lib/*"]
//    excludes = ["**/*.min.js", "**/*"]
}

def gitSha() {
    execOutput 'git rev-parse --short HEAD'
}

def gitTimestamp() {
    execOutput 'git log -n 1 --format=%at'
}

def gitBranch() {
    execOutput 'git rev-parse --abbrev-ref HEAD'
}

String execOutput(String exec) {
    def p = exec.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        final text = p.errorStream.text
        logger.warn(text)
        return text
    }
    return p.text.trim()
}

def envProp(String name, defaultValue = 'UNKNOWN' ) {
    System.getenv(name) ?: System.getProperty(name) ?: (defaultValue instanceof Closure ? defaultValue() : defaultValue)?.toString()
}

processResources {
    filesMatching("**/*.yml") {
        filter(ReplaceTokens, tokens: [
                'info.build.ci' : envProp('CI', 'false'),
                'info.build.date' : new Date().toString(),
                'info.build.jdk' : envProp('TRAVIS_JDK_VERSION', System.getProperty('java.version')),
                'info.build.number' : envProp('TRAVIS_BUILD_NUMBER'),
                'info.git.branch' : envProp('TRAVIS_BRANCH', { gitBranch() }),
                'info.git.commit' : envProp('TRAVIS_COMMIT', { gitSha() }),
                'info.git.slug' : envProp('TRAVIS_REPO_SLUG'),
                'info.git.tag' : envProp('TRAVIS_TAG'),
                'info.git.timestamp' : gitTimestamp()
        ])
    }
}

publish {
    dependsOn assemble
}

